# 字节码

## 类文件来源
> 1. 从本地系统中加载
> 2. 从网络下载 class 文件
> 3. 从 zip，jar 等归档文件中加载 class 文件
> 4. 从专有数据库中提取 class 文件
> 5. 将 Java 源文件动态编译为 class 文件

```sh
jar cvf hello.jar Hello.class
```

## 字节码

```java
public class HelloByteCode {
    public static void main(String[] args) {
        HelloByteCode helloByteCode = new HelloByteCode();
    }
}
```

```sh
javac HelloByteCode.java

javac -g HelloByteCode.java
```

> 使用包名或者相对路径都可以反编译成功

```sh
javap -c -verbose HelloByteCode
```

> 反编译后的代码清单中，有一个默认的构造函数以及 main 方法。这是因为，如果不定义任何构造函数，就会有一个默认的无参构造函数。自动生成的构造函数，其方法体应该是空的，但这里有一些指令，这是因为每个构造函数中都会先调用 super 类的构造函数，因此默认构造函数中就有一些相应的字节码指令

### 字节码格式

```
Classfile /Users/pain/Documents/java/java-learning/target/classes/com/pain/white/bytecode/HelloByteCode.class
  Last modified 2021年3月14日; size 473 bytes
  MD5 checksum 63705f42a11aa5746ebb7786f99fd6ff
  Compiled from "HelloByteCode.java"
public class com.pain.white.bytecode.HelloByteCode
  minor version: 0
  major version: 52
  flags: (0x0021) ACC_PUBLIC, ACC_SUPER
  this_class: #2                          // com/pain/white/bytecode/HelloByteCode
  super_class: #4                         // java/lang/Object
  interfaces: 0, fields: 0, methods: 2, attributes: 1
Constant pool:
   #1 = Methodref          #4.#19         // java/lang/Object."<init>":()V
   #2 = Class              #20            // com/pain/white/bytecode/HelloByteCode
   #3 = Methodref          #2.#19         // com/pain/white/bytecode/HelloByteCode."<init>":()V
   #4 = Class              #21            // java/lang/Object
   #5 = Utf8               <init>
   #6 = Utf8               ()V
   #7 = Utf8               Code
   #8 = Utf8               LineNumberTable
   #9 = Utf8               LocalVariableTable
  #10 = Utf8               this
  #11 = Utf8               Lcom/pain/white/bytecode/HelloByteCode;
  #12 = Utf8               main
  #13 = Utf8               ([Ljava/lang/String;)V
  #14 = Utf8               args
  #15 = Utf8               [Ljava/lang/String;
  #16 = Utf8               helloByteCode
  #17 = Utf8               SourceFile
  #18 = Utf8               HelloByteCode.java
  #19 = NameAndType        #5:#6          // "<init>":()V
  #20 = Utf8               com/pain/white/bytecode/HelloByteCode
  #21 = Utf8               java/lang/Object
{
  public com.pain.white.bytecode.HelloByteCode();
    descriptor: ()V
    flags: (0x0001) ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 3: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lcom/pain/white/bytecode/HelloByteCode;

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: (0x0009) ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=2, args_size=1
         0: new           #2                  // class com/pain/white/bytecode/HelloByteCode
         3: dup
         4: invokespecial #3                  // Method "<init>":()V
         7: astore_1
         8: return
      LineNumberTable:
        line 5: 0
        line 6: 8
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       9     0  args   [Ljava/lang/String;
            8       1     1 helloByteCode   Lcom/pain/white/bytecode/HelloByteCode;
}
SourceFile: "HelloByteCode.java"
```

> 其中显示了很多关于 class 文件信息：编译时间，MD5 校验和，符合的 Java 版本等

> ACC_PUBLIC 标志表示这个类是 public 类
> ACC_SUPER 标志是由于历史原因，JDK1.0 的 BUG 修正中引入的，用来修正 invokespecial 指令调用 super 类方法的问题，从 Java 1.1 开始，编译器一般都会自动生成 ACC_SUPER 标志

> 常量池就是一个常量的大字典，使用编号的方式把程序里用到的各类常量统一管理起来，这样在字节码操作里，只需要引用编号即可。很多指令后面使用了 #1, #2, #3 这样的编号，这就是对常量池的引用。常量池中的常量定义可以进行组合, 一个常量的定义中可以引用其他常量

#### 方法信息
> 方法描述：([Ljava/lang/String;)V

> 1. 小括号内是入参信息/形参信息
> 2. 左方括号表述数组
> 3. L 表示对象
> 4. java/lang/String 是类名称
> 5. 小括号后面的 V 则表示这个方法的返回值是 void

> 方法的访问标志 ACC_PUBLIC，ACC_STATIC 分别表示 public 和 static

> stack=2, locals=2, args_size=1 分别表示执行方法需要的栈的深度，局部变量表中保留的槽位个数，方法的参数个数

> 无参构造函数的方法描述是 stack=1, locals=1, args_size=1
> 参数个数是 1 的原因是：对于非静态方法，this 将被分配到局部变量表的第 0 号槽位中

#### 方法体
> JVM 是一台基于栈的计算机器，每个线程都有一个独属于自己的线程栈，用于存储栈帧。每一次方法调用，JVM 都会自动创建一个栈帧。栈帧由操作数栈，局部变量数组以及一个 class 引用组成。class 引用指向当前方法在运行时常量池中对应的 class

> 局部变量数组也称为局部变量表，其中包含了方法的参数，以及局部变量。局部变量数组的大小在编译时就已经确定：和局部变量 + 形参的个数有关，还要看每个变量/参数占用多少个字节

> 操作数栈是一个 LIFO 结构的栈，用于压入和弹出值，它的大小也在编译时确定

> 有一些操作码/指令可以将值压入操作数栈；还有一些操作码/指令则是从栈中获取操作数，并进行处理，再将结果压入栈；操作数栈还用于接收调用其他方法时返回的结果值

```
0: new           #2                  // class HelloByteCode
3: dup
4: invokespecial #3                  // Method "<init>":()V
7: astore_1
8: return
```

> 方法体中的字节码指令前面的数字之间的间隔不相等，原因是，有一部分操作码会附带有操作数，也会占用字节码数组中的空间。例如，new 就会占用三个槽位：一个用于存放操作码指令自身，两个用于存放操作数

#### 局部变量表
```java
public class MovingAverage {
    private int count = 0;
    private double sum = 0.0D;

    public void submit(double value) {
        this.count++;
        this.sum += value;
    }

    public double getAvg() {
        if (0 == this.count) {
            return sum;
        }

        return this.sum / this.count;
    }
}

public class LocalVariableTest {
    public static void main(String[] args) {
        MovingAverage ma = new MovingAverage();
        int num1 = 1;
        int num2 = 2;
        ma.submit(num1);
        ma.submit(num2);
        double avg = ma.getAvg();
    }
}
```

```
Classfile /Users/pain/Documents/java/lab/LocalVariableTest.class
  Last modified 2020-10-24; size 614 bytes
  MD5 checksum eb6bb9c0d6a1a6f0027bd30f3f6b1a6d
  Compiled from "LocalVariableTest.java"
public class LocalVariableTest
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #7.#28         // java/lang/Object."<init>":()V
   #2 = Class              #29            // MovingAverage
   #3 = Methodref          #2.#28         // MovingAverage."<init>":()V
   #4 = Methodref          #2.#30         // MovingAverage.submit:(D)V
   #5 = Methodref          #2.#31         // MovingAverage.getAvg:()D
   #6 = Class              #32            // LocalVariableTest
   #7 = Class              #33            // java/lang/Object
   #8 = Utf8               <init>
   #9 = Utf8               ()V
  #10 = Utf8               Code
  #11 = Utf8               LineNumberTable
  #12 = Utf8               LocalVariableTable
  #13 = Utf8               this
  #14 = Utf8               LLocalVariableTest;
  #15 = Utf8               main
  #16 = Utf8               ([Ljava/lang/String;)V
  #17 = Utf8               args
  #18 = Utf8               [Ljava/lang/String;
  #19 = Utf8               ma
  #20 = Utf8               LMovingAverage;
  #21 = Utf8               num1
  #22 = Utf8               I
  #23 = Utf8               num2
  #24 = Utf8               avg
  #25 = Utf8               D
  #26 = Utf8               SourceFile
  #27 = Utf8               LocalVariableTest.java
  #28 = NameAndType        #8:#9          // "<init>":()V
  #29 = Utf8               MovingAverage
  #30 = NameAndType        #34:#35        // submit:(D)V
  #31 = NameAndType        #36:#37        // getAvg:()D
  #32 = Utf8               LocalVariableTest
  #33 = Utf8               java/lang/Object
  #34 = Utf8               submit
  #35 = Utf8               (D)V
  #36 = Utf8               getAvg
  #37 = Utf8               ()D
{
  public LocalVariableTest();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 1: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   LLocalVariableTest;

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=6, args_size=1
         0: new           #2                  // class MovingAverage
         3: dup
         4: invokespecial #3                  // Method MovingAverage."<init>":()V
         7: astore_1
         8: iconst_1
         9: istore_2
        10: iconst_2
        11: istore_3
        12: aload_1
        13: iload_2
        14: i2d
        15: invokevirtual #4                  // Method MovingAverage.submit:(D)V
        18: aload_1
        19: iload_3
        20: i2d
        21: invokevirtual #4                  // Method MovingAverage.submit:(D)V
        24: aload_1
        25: invokevirtual #5                  // Method MovingAverage.getAvg:()D
        28: dstore        4
        30: return
      LineNumberTable:
        line 3: 0
        line 4: 8
        line 5: 10
        line 6: 12
        line 7: 18
        line 8: 24
        line 9: 30
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      31     0  args   [Ljava/lang/String;
            8      23     1    ma   LMovingAverage;
           10      21     2  num1   I
           12      19     3  num2   I
           30       1     4   avg   D
}
SourceFile: "LocalVariableTest.java"
```

> 编号 0 的字节码 new , 创建 MovingAverage 类的对象
> 编号 3 的字节码 dup 复制栈顶引用值
> 编号 4 的字节码 invokespecial 执行对象初始化
> 编号 7 使用 astore_1 指令将引用地址值存储到编号为 1 的局部变量中，astore_1 中的 1 指代 LocalVariableTable 中 ma 对应的槽位编号
> 编号 8 开始的指令，iconst_1 和 iconst_2 用来将常量值 1 和 2 加载到栈里面，并分别由指令 istore_2 和 istore_3 将它们存储到在槽位 2 和槽位 3 中

> store 之类的指令调用实际上从栈顶删除了一个值，因此再次使用相同值时，必须再加载一次。在上面的字节码中，调用 submit 方法之前，必须再次将参数值加载到栈中

> 调用 getAvg() 方法后，返回的结果位于栈顶，然后使用 dstore 将 double 值保存到本地变量 4 号槽位

> 在 LocalVariableTable 中，最前面的槽位会被方法参数占用。如果是静态方法，槽位 0 中并没有设置为 this 引用的地址；如果是非静态方法，this 会将分配到第 0 号槽位中

> 给局部变量赋值时，需要使用相应的指令来进行 store，这类指令都会删除栈顶值。相应的 load 指令则会将值从局部变量表压入操作数栈，但并不会删除局部变量中的值

## 操作码
> Java 字节码由单字节的指令组成，理论上最多支持 256 个操作码。实际上只使用了 200 左右的操作码，还有一些操作码则保留给调试操作

> 操作码又称为指令，主要由类型前缀和操作名称两部分组成。根据指令的性质，主要分为四个大类：
> 1. 栈操作指令，包括与局部变量交互的指令
> 2. 程序流程控制指令
> 3. 对象操作指令，包括方法调用指令
> 4. 算术运算以及类型转换指令
> 5. 执行专门任务的指令，比如同步指令，以及抛出异常相关的指令等等

### 初始化指令
```
0: new           #2                  // class HelloByteCode
3: dup
4: invokespecial #3                  // Method "<init>":()V
```

> new 指令只是创建对象，但没有调用构造函数
> invokespecial 指令用来调用某些特殊方法的，这里调用的是构造函数
> dup 指令用于复制栈顶的值。由于构造函数调用不会返回值，所以如果没有 dup 指令，在对象上调用方法并初始化之后，操作数栈就会是空的

> 有了 dup 指令，在构造函数返回之后，可以将对象实例赋值给局部变量或某个字段。因此，接下来的那条指令一般是以下几种:
> 1. astore {N} or astore_{N}  赋值给局部变量，其中 {N} 是局部变量表中的位置
> 2. putfield – 将值赋给实例字段
> 3. putstatic – 将值赋给静态字段

> 在调用构造函数的时候，其实还会执行另一个类似的方法 ，甚至在执行构造函数之前就执行了。还有一个可能执行的方法是该类的静态初始化方法 ，但并不能被直接调用，而是由这些指令触发的：new, getstatic, putstatic, invokestatic

### 栈内存操作
> 最基础的是 dup 和 pop 指令。dup 指令复制栈顶元素的值。pop 指令则从栈中删除最顶部的值。还有复杂一点的指令：
> 1. swap 指令可交换栈顶两个元素的值
> 2. dup_x1 复制栈顶的值，并将复制的值插入到最上面 2 个值的下方
> 3. dup2_x1 复制栈顶 1 个 64 位或 2 个 32 位的值，并将复制的值按照原始顺序，插入原始值下面一个 32 位值的下方

> 交换 2 个 double 类型的值，一个 double 值占两个槽位，如果栈中有两个 double 值，将占用 4 个槽位。如果要执行交换，就不能使用 swap 指令，因为 swap 只适用于单字。解决方法就是使用 dup2_x2 指令，将操作数栈顶部的 double 值, 复制到栈底 double 值的下方，然后再使用 pop2 指令弹出栈顶的 double 值

### 流程控制指令
```java
public class ForLoopTest {
    private static int[] numbers = {1, 6, 8};

    public static void main(String[] args) {
        MovingAverage ma = new MovingAverage();

        for (int number : numbers) {
            ma.submit(number);
        }

        double avg = ma.getAvg();
    }
}
```

```
Classfile /Users/pain/Documents/java/lab/ForLoopTest.class
  Last modified 2020-10-24; size 752 bytes
  MD5 checksum 3541beb0cac3f30f684d9b7952006566
  Compiled from "ForLoopTest.java"
public class ForLoopTest
  minor version: 0
  major version: 52
  flags: ACC_PUBLIC, ACC_SUPER
Constant pool:
   #1 = Methodref          #8.#35         // java/lang/Object."<init>":()V
   #2 = Class              #36            // MovingAverage
   #3 = Methodref          #2.#35         // MovingAverage."<init>":()V
   #4 = Fieldref           #7.#37         // ForLoopTest.numbers:[I
   #5 = Methodref          #2.#38         // MovingAverage.submit:(D)V
   #6 = Methodref          #2.#39         // MovingAverage.getAvg:()D
   #7 = Class              #40            // ForLoopTest
   #8 = Class              #41            // java/lang/Object
   #9 = Utf8               numbers
  #10 = Utf8               [I
  #11 = Utf8               <init>
  #12 = Utf8               ()V
  #13 = Utf8               Code
  #14 = Utf8               LineNumberTable
  #15 = Utf8               LocalVariableTable
  #16 = Utf8               this
  #17 = Utf8               LForLoopTest;
  #18 = Utf8               main
  #19 = Utf8               ([Ljava/lang/String;)V
  #20 = Utf8               number
  #21 = Utf8               I
  #22 = Utf8               args
  #23 = Utf8               [Ljava/lang/String;
  #24 = Utf8               ma
  #25 = Utf8               LMovingAverage;
  #26 = Utf8               avg
  #27 = Utf8               D
  #28 = Utf8               StackMapTable
  #29 = Class              #23            // "[Ljava/lang/String;"
  #30 = Class              #36            // MovingAverage
  #31 = Class              #10            // "[I"
  #32 = Utf8               <clinit>
  #33 = Utf8               SourceFile
  #34 = Utf8               ForLoopTest.java
  #35 = NameAndType        #11:#12        // "<init>":()V
  #36 = Utf8               MovingAverage
  #37 = NameAndType        #9:#10         // numbers:[I
  #38 = NameAndType        #42:#43        // submit:(D)V
  #39 = NameAndType        #44:#45        // getAvg:()D
  #40 = Utf8               ForLoopTest
  #41 = Utf8               java/lang/Object
  #42 = Utf8               submit
  #43 = Utf8               (D)V
  #44 = Utf8               getAvg
  #45 = Utf8               ()D
{
  public ForLoopTest();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 1: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   LForLoopTest;

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=6, args_size=1
         0: new           #2                  // class MovingAverage
         3: dup
         4: invokespecial #3                  // Method MovingAverage."<init>":()V
         7: astore_1
         8: getstatic     #4                  // Field numbers:[I
        11: astore_2
        12: aload_2
        13: arraylength
        14: istore_3
        15: iconst_0
        16: istore        4
        18: iload         4
        20: iload_3
        21: if_icmpge     43
        24: aload_2
        25: iload         4
        27: iaload
        28: istore        5
        30: aload_1
        31: iload         5
        33: i2d
        34: invokevirtual #5                  // Method MovingAverage.submit:(D)V
        37: iinc          4, 1
        40: goto          18
        43: aload_1
        44: invokevirtual #6                  // Method MovingAverage.getAvg:()D
        47: dstore_2
        48: return
      LineNumberTable:
        line 5: 0
        line 7: 8
        line 8: 30
        line 7: 37
        line 11: 43
        line 12: 48
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
           30       7     5 number   I
            0      49     0  args   [Ljava/lang/String;
            8      41     1    ma   LMovingAverage;
           48       1     2   avg   D
      StackMapTable: number_of_entries = 2
        frame_type = 255 /* full_frame */
          offset_delta = 18
          locals = [ class "[Ljava/lang/String;", class MovingAverage, class "[I", int, int ]
          stack = []
        frame_type = 248 /* chop */
          offset_delta = 24

  static {};
    descriptor: ()V
    flags: ACC_STATIC
    Code:
      stack=4, locals=0, args_size=0
         0: iconst_3
         1: newarray       int
         3: dup
         4: iconst_0
         5: iconst_1
         6: iastore
         7: dup
         8: iconst_1
         9: bipush        6
        11: iastore
        12: dup
        13: iconst_2
        14: bipush        8
        16: iastore
        17: putstatic     #4                  // Field numbers:[I
        20: return
      LineNumberTable:
        line 2: 0
}
SourceFile: "ForLoopTest.java"
```

> 0 号槽位被 main 方法的参数 args 占据了
> 1 号槽位被 ma 占用了
> 5 号槽位被 number 占用了
> 2 号槽位是 for 循环之后才被 avg 占用的

> 通过分析字节码指令可以看出，在 2 , 3 , 4 槽位有 3 个匿名的局部变量（astore_2, istore_3, istore 4 等指令）

> 2 号槽位的变量保存了 numbers 的引用值
> 3 号槽位的变量，由 arraylength 指令使用，得出循环的长度
> 4 号槽位的变量，是循环计数器，每次迭代后使用 iinc 指令来递增

> 循环体中的第一条指令用于执行循环计数器与数组长度的比较

```
18: iload         4
20: iload_3
21: if_icmpge     43
```

> 这段指令将局部变量表中 4 号槽位 和 3 号槽位的值加载到栈中，并调用 if_icmpge 指令来比较他们的值。如果一个数的值大于或等于另一个值，则程序执行流程跳转到 pc=43 的地方继续执行；如果条件不成立，则循环进行下一次迭代

### 算术运算指令
> 唯一不需要将数值 load 到操作数栈的指令是 iinc，它可以直接对 LocalVariableTable 中的值进行运算

### 类型转换指令

### 方法调用指令和参数传递
> invokestatic，用于调用某个类的静态方法，这是方法调用指令中最快的一个
> invokespecial，用来调用构造函数、同一个类中的 private 方法，以及可见的超类方法
> invokevirtual，如果是具体类型的目标对象，用于调用公共、受保护和打包私有方法
> invokeinterface，当要调用的方法属于某个接口时，将使用该指令

> 之所以需要 invokevirtual 和 invokeinterface 这两种指令，是源于对方法调用的优化。JVM 必须先解析该方法，然后才能调用它
> 1. 使用 invokestatic 指令，JVM 就确切地知道要调用的是哪个方法，因为调用的是静态方法，只能属于一个类
> 2. 使用 invokespecial 时，查找的数量也很少，解析也更加容易，运行时就能更快地找到所需的方法

> A 类包含 method1 和 method2 方法；子类 B 继承 A，继承了 method1，覆写了 method2，并声明了方法 method3。请注意，method1 和 method2 方法在类 A 和类 B 中处于相同的索引位置

> 那么，在运行时只要调用 method2，一定是在位置 2 处找到它。假设有一个接口 X 声明了 methodX 方法，让 B 类在上面的基础上实现接口 X。新方法 methodX 位于索引 4 处，在这种情况下，它看起来与 method3 没什么不同。但如果还有另一个类 C 也实现了 X 接口，但不继承 A，也不继承 B，类 C 中的接口方法位置与类 B 的不同

> 与 invokinterface 相比，invokevirtual 针对具体的类型方法表是固定的，所以每次都可以精确查找，效率更高

> JDK7 的字节码指令集新增了 invokedynamic 指令。这条新增加的指令是实现动态类型语言支持而进行的改进之一，同时也是 JDK8 以后支持的 lambda 表达式的实现基础

> invokedynamic 指令配合新增的方法句柄（用来描述一个跟类型无关的方法的签名，甚至不包括方法名称），可以在运行时再决定由哪个类来接收被调用的方法